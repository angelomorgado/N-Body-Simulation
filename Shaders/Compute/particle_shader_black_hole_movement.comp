#version 460 core

layout (local_size_x = 64) in;

layout(std430, binding = 0) buffer _vertexPosition {
    vec4 vertexPosition[];
};
layout(std430, binding = 1) buffer _vertexSize{
    float vertexSize[];
};
layout(std430, binding = 2) buffer _vertexRadius{
    float vertexRadius[];
};
layout(binding = 3, std430) buffer _vertexColor{
    vec4 vertexColor[];
};
layout(std430, binding = 4) buffer _vertexVelocity{
    vec4 vertexVelocity[];
};
layout(std430, binding = 5) buffer _vertexMass{
    float vertexMass[];
};

// #-------------Black Holes Motion------------#

// Uniforms
uniform vec3 first_black_hole_position;
uniform vec3 second_black_hole_position;
uniform float first_g_force;
uniform float second_g_force;

float deltaTime = 0.0005;
float max_distance = 50.0;

void black_hole(uint id)
{
    // Force from first black hole
    vec3 force = vec3(0.0);
    vec3 dist = first_black_hole_position - vertexPosition[id].xyz;
    force = (first_g_force / length(dist)) * normalize(dist);

    // Force from second black hole
    dist = second_black_hole_position - vertexPosition[id].xyz;
    force += (second_g_force / length(dist)) * normalize(dist);

    // Reset particles that go to far away from the black hole (attraction point)
    if(length(dist) > max_distance)
    {
        vertexPosition[id].xyz = vec3(0.0,0.0,0.0);
    }
    else
    {
        vec3 acceleration = force * 1.0/vertexMass[id];

        // Update position
        vertexPosition[id].xyz = vec3(vertexPosition[id].xyz + (vertexVelocity[id].xyz * deltaTime) + (0.5 * acceleration * dot(deltaTime,deltaTime)));
        vertexVelocity[id].xyz = vec3(vertexVelocity[id].xyz + (acceleration * deltaTime));
    }
}

// #--------------------------------------#

void main()
{ 
    uint id = gl_GlobalInvocationID.x;
    
    // change the color of the particle to a random color
    vertexColor[id] = vec4(sin(vertexPosition[id].x), cos(vertexPosition[id].y), sin(vertexPosition[id].z), 1.0);

    // black holes motions
    black_hole(id);
}